FUNCTION_BLOCK heart_attack_prediction

VAR_INPUT
    age : REAL;
    chol : REAL;
    obesity : REAL;
END_VAR

VAR_OUTPUT
    risk : REAL;
END_VAR

FUZZIFY age
    TERM young := (20, 1) (30, 1) (40, 0);
    TERM middle := (30, 0) (45, 1) (60, 0);
    TERM old := (50, 0) (60, 1) (80, 1);
END_FUZZIFY

FUZZIFY chol
    TERM low := (100, 1) (150, 1) (200, 0);
    TERM medium := (150, 0) (225, 1) (300, 0);
    TERM high := (250, 0) (300, 1) (400, 1);
END_FUZZIFY

FUZZIFY obesity
    TERM low := (0, 1) (25, 1) (50, 0);
    TERM medium := (25, 0) (50, 1) (75, 0);
    TERM high := (50, 0) (75, 1) (100, 1);
END_FUZZIFY

DEFUZZIFY risk
    TERM low := (0, 1) (20, 1) (40, 0);
    TERM medium := (30, 0) (50, 1) (70, 0);
    TERM high := (60, 0) (80, 1) (100, 1);
    METHOD : COG;
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK rules
    AND : MIN;
    ACT : MIN;
    ACCU : MAX;

    RULE 1 : IF age IS young AND chol IS low AND obesity IS low THEN risk IS low;
    RULE 2 : IF age IS young AND chol IS medium AND obesity IS low THEN risk IS medium;
    RULE 3 : IF age IS young AND chol IS high AND obesity IS low THEN risk IS high;
    RULE 4 : IF age IS middle AND chol IS low AND obesity IS medium THEN risk IS low;
    RULE 5 : IF age IS middle AND chol IS medium AND obesity IS medium THEN risk IS medium;
    RULE 6 : IF age IS middle AND chol IS high AND obesity IS medium THEN risk IS high;
    RULE 7 : IF age IS old AND chol IS low AND obesity IS high THEN risk IS low;
    RULE 8 : IF age IS old AND chol IS medium AND obesity IS high THEN risk IS medium;
    RULE 9 : IF age IS old AND chol IS high AND obesity IS high THEN risk IS high;
END_RULEBLOCK

END_FUNCTION_BLOCK
